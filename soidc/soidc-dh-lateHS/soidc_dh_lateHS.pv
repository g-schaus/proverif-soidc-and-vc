(* --------------------------------------------------------------------------------------------------------- *)
(* Solid Open ID Connect With Diffie Hellman - Late Handshake Variant *)
(* --------------------------------------------------------------------------------------------------------- *)

(* --------------------------------------------------------------------------------------------------------- *)
(* Start Definitions *)
(* --------------------------------------------------------------------------------------------------------- *)

(* START keys *)
type SymmetricKey.
type SecretKey.
type PublicKey.
fun pk(SecretKey):PublicKey.
(* END keys *)

(* START group *)
type G.
type exponent.
fun exp(G, exponent): G.
fun g(exponent): G.
equation forall x: exponent, y: exponent; exp(g(x),y) = exp(g(y),x).
fun GtoSymK(G): SymmetricKey.
fun bitG(G):bitstring.
reduc forall elem:G; unbitG(bitG(elem))=elem.
(* END group *)

(* START hash *)
fun hash(bitstring): bitstring.
fun hash_pk(PublicKey): bitstring.
(* End hash *)

(* START symmetric encryption *)
fun enc(bitstring(*the message*), SymmetricKey):bitstring (*symmetric encrypt input msg*).
reduc forall msg:bitstring,symk:SymmetricKey; dec(enc(msg,symk),symk)=msg (*symmetric decrypt*).
(* END symmetric encryption *)

(* START asymmetric encryption *)
fun aenc(bitstring(*the message*), PublicKey):bitstring (*asymmetric encrypt input msg*).
reduc forall msg:bitstring,sk:SecretKey; adec(aenc(msg,pk(sk)),sk)=msg (*asymmetric decrypt*).
(* END Asymmetric encryption *)

(* START digital signature *)
fun sign(bitstring(*the message*), SecretKey):bitstring (*sign the input msg*).
reduc forall msg:bitstring,sk:SecretKey; check(sign(msg,sk),pk(sk))=msg (*check signature, and get msg*).
reduc forall msg:bitstring,sk:SecretKey; getMsg(sign(msg,sk))=msg (*just get msg*).
(* END digital signature *)

(*DEFS*)
fun ch(bitstring): channel.

(* SECRECY *)

const tag1:bitstring.
const tag2:bitstring.
const tag3:bitstring.
const tag4:bitstring.
const tag5:bitstring.
const tag6:bitstring.
const tag7:bitstring.
const tag8:bitstring.
const tag9:bitstring.
const tag10:bitstring.
const tag11:bitstring.
const tag12:bitstring.

free check_reach_app:bitstring [private].
free check_reach_browser:bitstring [private].
free check_reach_issuer:bitstring [private].

free free_code: bitstring [private].
free free_password: bitstring [private].

(* AUTHENTICATION *)

(* App *)
event appSendsLastMessageToIssuer(bitstring, bitstring, bitstring).
event appSendsLastMessageToBrowser(bitstring, bitstring).
event appCompletesProtocol(bitstring, bitstring, bitstring, bitstring, bitstring, bitstring, bitstring).

(* Browser *)
event browserSendsLastMessageToIssuer(bitstring, bitstring, bitstring).
event browserSendsLastMessageToApp(bitstring, bitstring, bitstring).
event browserCompletesProtocol(bitstring, bitstring, bitstring, bitstring, bitstring, bitstring).

(* Issuer *)
event issuerSendsLastMessageToApp(bitstring, bitstring, bitstring, bitstring).
event issuerSendsLastMessageToBrowser(bitstring, bitstring, bitstring, bitstring).
event issuerCompletesProtocol(bitstring, bitstring, bitstring, bitstring, bitstring, bitstring).

(* --------------------------------------------------------------------------------------------------------- *)
(* End Definitions *)
(* Start Agent Macros *)
(* --------------------------------------------------------------------------------------------------------- *)

let app(talksOnlyToHonest:bool, c:channel, a:bitstring, ska:SecretKey, b:bitstring, pkb:PublicKey, i:bitstring, pki:PublicKey) =

    (* !Talking to Browser (Without Handshake) *)

    (* Message 1 *)
    new pkce:bitstring;
    new scope:bitstring;
    let m_1 = (tag1, a, hash(pkce), scope) in
    out(c, m_1);

    (* ... Waiting For Issuer Response While It Talks To The Browser ... *)

    (* !Connecting to Browser *)

    (* Message 6 *)
    in(c, m_6:bitstring);
    let (tag6':bitstring, bitgx:bitstring) = m_6 in
    if (tag6 = tag6') then
    let gx = unbitG(bitgx) in

    (* Message 7 *)
    new y: exponent;
    let gy = g(y) in
    let k1 = GtoSymK(exp(gx, y)) in
    let m_7 = (tag7, bitG(gy), enc(sign((gy, gx), ska), k1)) in
    event appSendsLastMessageToBrowser(m_6, m_7);
    out(c, m_7);

    (* !Talking to Browser *)

    (* Message 8 *)
    in(c, m_8:bitstring);
    let (tag8':bitstring, code:bitstring) = dec(m_8, k1) in
    if (tag8 = tag8') then

    (* !Connecting to Issuer *)
 
    (* Message 9 *)
    new x:exponent;
    let gx = g(x) in
    let m_9 = (tag9, bitG(gx)) in
    out(c, m_9);

    (* Message 10 *)
    in(c, m_10:bitstring);
    let (tag10':bitstring, bit_gy:bitstring, enc_signature:bitstring) = m_10 in
    if (tag10 = tag10') then
    let gy = unbitG(bit_gy) in
    let k3 = GtoSymK(exp(gy, x)) in
    if (gy, gx) = check(dec(enc_signature, k3), pki) then

    (* !Talking to Issuer*)

    (* Message 11 *)
    new sks:SecretKey;
    new jti:bitstring;
    let T2 = (a, jti, code, pkce) in
    let m_11 = enc((tag11, pk(sks), sign(T2, sks)), k3) in
    event appSendsLastMessageToIssuer(m_9, m_10, m_11);
    out(c, m_11);

    (* Message 12 *)
    in(c, m_12:bitstring);
    let (tag12':bitstring, signed_M:bitstring) = dec(m_12, k3) in
    if (tag12 = tag12') then
    let M = check(signed_M, pki) in
    let (jtk:bitstring, jti':bitstring, scope':bitstring, a':bitstring, i':bitstring, b':bitstring) = M in

    if (jtk = hash_pk(pk(sks))) then
    if (jti = jti') then
    if (scope = scope') then
    if (a = a') then
    if (i = i') then
    if (b = b') then

    (* --------------------------------------------------------------------------------------- *)
    (* Query Preparations *)

    if talksOnlyToHonest then

    out(c, check_reach_app);

    event appCompletesProtocol(m_6, m_7, m_8, m_9, m_10, m_11, m_12);

    out(ch(code), free_code);

    0.

let browser(talksOnlyToHonest:bool, c:channel, b:bitstring, skb:SecretKey, a:bitstring, pka:PublicKey, i:bitstring, pki:PublicKey, password:bitstring) =

    (* !Talking to App (Without Handshake) *)

    (* Message 1 *)
    in(c, m_1:bitstring);
    let (tag1':bitstring, a':bitstring, h_pkce:bitstring, scope:bitstring) = m_1 in
    if (tag1 = tag1') then
    if (a = a') then

    (* !Connecting to Issuer *)

    (* Message 2 *)
    new x:exponent;
    let gx = g(x) in
    let m_2 = (tag2, bitG(gx)) in
    out(c, m_2);

    (* Message 3 *)
    in(c, m_3:bitstring);
    let (tag3':bitstring, bit_gy:bitstring, enc_signature:bitstring) = m_3 in
    if (tag3 = tag3') then
    let gy = unbitG(bit_gy) in
    let k2 = GtoSymK(exp(gy, x)) in
    if (gy, gx) = check(dec(enc_signature, k2), pki) then

    (* !Talking to Issuer *)

    (* Message 4 *)
    let m_4 = enc((tag4, a, scope, h_pkce, b, password), k2) in
    event browserSendsLastMessageToIssuer(m_2, m_3, m_4);
    out(c, m_4);

    (* Message 5 *)
    in(c, m_5:bitstring);
    let (tag5':bitstring, code:bitstring) = dec(m_5, k2) in
    if (tag5 = tag5') then

    (* !Connecting to App *)

    (* Message 6 *)
    new x:exponent;
    let gx = g(x) in
    let m_6 = (tag6, bitG(gx)) in
    out(c, m_6);

    (* Message 7 *)
    in(c, m_7:bitstring);
    let (tag7':bitstring, bit_gy:bitstring, enc_signature:bitstring) = m_7 in
    if (tag7 = tag7') then
    let gy = unbitG(bit_gy) in
    let k1 = GtoSymK(exp(gy, x)) in
    if (gy, gx) = check(dec(enc_signature, k1), pka) then

    (* !Talking to App *)

    (* Message 8 *)
    let m_8 = enc((tag8, code), k1) in
    event browserSendsLastMessageToApp(m_6, m_7, m_8);
    out(c, m_8);

    (* --------------------------------------------------------------------------------------- *)
    (* Query Preparations *)

    if talksOnlyToHonest then

    out(c, check_reach_browser);

    event browserCompletesProtocol(m_2, m_3, m_4, m_5, m_6, m_7);

    out(ch(code), free_code);
    out(ch(password), free_password);

    0.

let issuer(talksOnlyToHonest:bool, c:channel, i:bitstring, ski:SecretKey, a:bitstring, pka:PublicKey, b:bitstring, pkb:PublicKey, password:bitstring) =

    (* !Connecting to Browser *)

    (* Message 2 *)
    in(c, m_2:bitstring);
    let (tag2':bitstring, bitgx:bitstring) = m_2 in
    if (tag2 = tag2') then
    let gx = unbitG(bitgx) in

    (* Message 3 *)
    new y: exponent;
    let gy = g(y) in
    let k2 = GtoSymK(exp(gx, y)) in
    let m_3 = (tag3, bitG(gy), enc(sign((gy, gx), ski), k2)) in
    out(c, m_3);

    (* !Talking to Browser *)

    (* Message 4 *)
    in(c, m_4:bitstring);
    let (tag4':bitstring, a':bitstring, h_pkce:bitstring, scope:bitstring, b':bitstring, password':bitstring) = dec(m_4, k2) in
    if (tag4 = tag4') then

    if (a = a') then
    if (b = b') then
    if (password = password') then

    (* Message 5 *)
    new code:bitstring;
    let m_5 = enc((tag5, code), k2) in
    event issuerSendsLastMessageToBrowser(m_2, m_3, m_4, m_5);
    out(c, m_5);

    (* Waiting for Browser to transfer code to App *)

    (* !Connecting to App *)
     
    (* Message 9 *)
    in(c, m_9:bitstring);
    let (tag9':bitstring, bitgx:bitstring) = m_9 in
    if (tag9 = tag9') then
    let gx = unbitG(bitgx) in

    (* Message 10 *)
    new y: exponent;
    let gy = g(y) in
    let k3 = GtoSymK(exp(gx, y)) in
    let m_10 = (tag10, bitG(gy), enc(sign((gy, gx), ski), k3)) in
    out(c, m_10);

    (* !Talking to App*)

    (* Message 11 *)
    in(c, m_11:bitstring);
    let (tag11':bitstring, pksks:PublicKey, signature:bitstring) = dec(m_11, k3) in
    if (tag11 = tag11') then
    let (a'':bitstring, jti:bitstring, code':bitstring, pkce:bitstring) = check(signature, pksks) in

    if (a = a'') then
    if (code = code') then
    if (h_pkce = hash(pkce)) then

    (* Message 12 *)
    let jtk = hash_pk(pksks) in
    let M = (jtk, jti, scope, a, i, b) in
    let m_12 = enc((tag12, sign(M, ski)), k3) in
    event issuerSendsLastMessageToApp(m_9, m_10, m_11, m_12);
    out(c, m_12);

    (* --------------------------------------------------------------------------------------- *)
    (* Query Preparations *)

    if talksOnlyToHonest then

    out(c, check_reach_issuer);

    event issuerCompletesProtocol(m_2, m_3, m_4, m_9, m_10, m_11);

    out(ch(code), free_code);
    out(ch(password), free_password);

    0.

(* --------------------------------------------------------------------------------------------------------- *)
(* End Agent Macros *)
(* Start Open Network *)
(* --------------------------------------------------------------------------------------------------------- *)

let open_app(c:channel, a:bitstring, ska:SecretKey, b:bitstring, pkb:PublicKey, i:bitstring, pki:PublicKey) =
  in(c, e1:bitstring);
  in(c, e2:bitstring);
  if (e1 = b) && (e2 = i) then
    app(true, c, a, ska, b, pkb, i, pki)
  else
  if (e1 <> b) && (e2 = i) then
    in(c, pke1:PublicKey);
    app(false, c, a, ska, e1, pke1, i, pki)
  else
  if (e1 = b) && (e2 <> i) then
    in(c, pke2:PublicKey);
    app(false, c, a, ska, b, pkb, e2, pke2)
  else
  if (e1 <> b) && (e2 <> i) then
    in(c, pke1:PublicKey);
    in(c, pke2:PublicKey);
    app(false, c, a, ska, e1, pke1, e2, pke2)
  .

let open_browser(c:channel, b:bitstring, skb:SecretKey, a:bitstring, pka:PublicKey, i:bitstring, pki:PublicKey, password:bitstring) =
  in(c, e1:bitstring);
  in(c, e2:bitstring);
  if (e1 = a) && (e2 = i) then
    browser(true, c, b, skb, a, pka, i, pki, password)
  else
  if (e1 <> a) && (e2 = i) then
    in(c, pke1:PublicKey);
    browser(false, c, b, skb, e1, pke1, i, pki, password)
  else
  if (e1 = a) && (e2 <> i) then
    in(c, pke2:PublicKey);
    new wrong_pw:bitstring;
    browser(false, c, b, skb, a, pka, e2, pke2, wrong_pw)
  else
  if (e1 <> a) && (e2 <> i) then
    in(c, pke1:PublicKey);
    in(c, pke2:PublicKey);
    new wrong_pw:bitstring;
    browser(false, c, b, skb, e1, pke1, e2, pke2, wrong_pw)
  .

let open_issuer(c:channel, i:bitstring, ski:SecretKey, a:bitstring, pka:PublicKey, b:bitstring, pkb:PublicKey, password:bitstring) =
  in(c, e1:bitstring);
  in(c, e2:bitstring);
  if (e1 = a) && (e2 = b) then
    issuer(true, c, i, ski, a, pka, b, pkb, password)
  else
  if (e1 <> a) && (e2 = b) then
    in(c, pke1:PublicKey);
    issuer(false, c, i, ski, e1, pke1, b, pkb, password)
  else
  if (e1 = a) && (e2 <> b) then
    new wrong_pw:bitstring;
    in(c, pke2:PublicKey);
    issuer(false, c, i, ski, a, pka, e2, pke2, wrong_pw)
  else
  if (e1 <> a) && (e2 <> b) then
    new wrong_pw:bitstring;
    in(c, pke1:PublicKey);
    in(c, pke2:PublicKey);
    issuer(false, c, i, ski, e1, pke1, e2, pke2, wrong_pw)
  .

(* --------------------------------------------------------------------------------------------------------- *)
(* End Open Network *)
(* Start Assumptions *)
(* --------------------------------------------------------------------------------------------------------- *)

(* SECRECY *)

(* query attacker(check_reach_app).
query attacker(check_reach_browser).
query attacker(check_reach_issuer). *)

(* query attacker(free_code).
query attacker(free_password). *)

(* AUTHENTICATION *)

(* App Completes Protocol *)
(* query m_6:bitstring, m_7:bitstring, m_8:bitstring, m_9:bitstring, m_10:bitstring, m_11:bitstring, m_12:bitstring;
    inj-event(appCompletesProtocol(m_6, m_7, m_8, m_9, m_10, m_11, m_12))
    ==>
    inj-event(browserSendsLastMessageToApp(m_6, m_7, m_8))
    &&
    inj-event(issuerSendsLastMessageToApp(m_9, m_10, m_11, m_12))
    . *)
    
(* Browser Completes Protocol *)
(* query m_2:bitstring, m_3:bitstring, m_4:bitstring, m_5:bitstring, m_6:bitstring, m_7:bitstring; 
    inj-event(browserCompletesProtocol(m_2, m_3, m_4, m_5, m_6, m_7))
    ==>
    inj-event(appSendsLastMessageToBrowser(m_6, m_7))
    &&
    inj-event(issuerSendsLastMessageToBrowser(m_2, m_3, m_4, m_5))
    . *)

(* Issuer Completes Protocol *)
query m_2:bitstring, m_3:bitstring, m_4:bitstring, m_9:bitstring, m_10:bitstring, m_11:bitstring; 
    inj-event(issuerCompletesProtocol(m_2, m_3, m_4, m_9, m_10, m_11))
    ==>
    inj-event(appSendsLastMessageToIssuer(m_9, m_10, m_11))
    &&
    inj-event(browserSendsLastMessageToIssuer(m_2, m_3, m_4))
    .

(* --------------------------------------------------------------------------------------------------------- *)
(* End Assumptions *)
(* Start Main *)
(* --------------------------------------------------------------------------------------------------------- *)

free c:channel.
process (*use root process for setup *)

    (* create identities *)
    new a:bitstring;
    new b:bitstring;
    new i:bitstring;

    (*advertise identities*)
    out(c,a);
    out(c,b);
    out(c,i);

    (*declare secret keys*)
    new ska:SecretKey;
    new skb:SecretKey;
    new ski:SecretKey;

    (*advertise public keys*)
    out(c,pk(ska));
    out(c,pk(skb));
    out(c,pk(ski));

    (*generate variables*)
    new password:bitstring;

    (*let agents dance*)
    (
        ! open_app      (c, a, ska, b, pk(skb), i, pk(ski))
      | ! open_browser  (c, b, skb, a, pk(ska), i, pk(ski), password)
      | ! open_issuer   (c, i, ski, a, pk(ska), b, pk(skb), password)
    )

(* --------------------------------------------------------------------------------------------------------- *)
(* End Main *)
(* --------------------------------------------------------------------------------------------------------- *)
